apply plugin: 'com.android.library'

private String getNativeLibraryName() {
  def libraryName = System.getenv('LIBABP_SHARED_LIBRARY_NAME')
  return libraryName != null ? libraryName : 'adblockplus-jni'
}

android {
  compileSdkVersion 29
  buildToolsVersion "29.0.0"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    buildConfigField "String", "nativeLibraryName", '"' + getNativeLibraryName() + '"'
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'proguard-rules-adblock.txt'
  }

  flavorDimensions "abi"

  productFlavors {
    abi_arm {
      dimension = "abi"
      ndk {
        abiFilters "armeabi-v7a"
      }

      externalNativeBuild {
        ndkBuild {
          arguments "APP_ABI=armeabi-v7a"
        }
      }
    }

    abi_all {
      dimension = "abi"
      ndk {
        abiFilters 'armeabi-v7a'
      }
    }
  }


  if (System.getenv('SKIP_JNI_COMPILATION') != null) {
    println "[Configuration] Skipping JNI compilation"
    sourceSets.main.jni.srcDirs = []
  } else {
    externalNativeBuild {
      ndkBuild {
        path 'src/main/jni/Android.mk'
      }
    }
  }

  def sharedV8LibFiles = System.getenv('SHARED_V8_LIB_FILENAMES')
  if (sharedV8LibFiles != null) {
    def sharedV8LibFilesSet = []
    def sharedV8LibFilesArray = sharedV8LibFiles.split(' ')
    sharedV8LibFilesArray.each { eachFileName ->
      sharedV8LibFilesSet.add("**/" + eachFileName)
      println "[Configuration] Excluding shared v8 library " + eachFileName + " from AAR"
    }

    packagingOptions {
      excludes = sharedV8LibFilesSet
    }
  } else {
    println "[Configuration] No shared v8 libraries excluded from AAR"
  }
}

dependencies {
  api 'com.jakewharton.timber:timber:4.7.1'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test:core:1.1.0'
  androidTestImplementation 'androidx.test:runner:1.1.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.0'
  androidTestImplementation 'org.mockito:mockito-core:2.24.0'
  androidTestImplementation 'org.mockito:mockito-android:2.24.0'
}
